buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$KOTLIN_VERSION"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco-android'

repositories {
    mavenCentral()
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        applicationId "lt.neworld.randomdecision2"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.0.1"
    }

    signingConfigs {
        release {
            def propFile = file("key.properties")
            if (propFile.canRead()) {
                def prop = new Properties()
                prop.load(new FileInputStream(propFile))

                storeFile file(prop['PATH'])
                keyAlias prop['ALIAS']
                storePassword prop['PSW']
                keyPassword prop['PSW']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        warning "InvalidPackage"
        abortOnError false
    }
    packagingOptions {
        exclude "META-INF/LICENSE"
    }
}

dependencies {
    compile project(":shared")
    compile "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"
    compile 'com.dropbox.core:dropbox-core-sdk:2.1.2'
    compile 'io.reactivex:rxjava:1.2.1'
    compile 'io.reactivex:rxandroid:1.2.1@aar'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'

    testCompile "org.jetbrains.kotlin:kotlin-test:$KOTLIN_VERSION"
    testCompile 'org.robolectric:robolectric:3.1.2'
    testCompile 'junit:junit:4.12'
}

jacocoAndroidUnitTestReport {
    excludes += ['**/*$$*']
}

check.dependsOn jacocoTestReport
